# vim: set ft=zsh:
#
# A minimal theme that uses git-info-fast for (slightly) less info for
# (much) better performance.
#
# Authors:
#   Patrick Brisbin <pbrisbin@gmail.com>
#
# Screenshots:
#   http://pbrisbin.com/static/screenshots/desktop_1204141330.png
#

function prompt_pbrisbin_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info-fast] )); then
    git-info-fast
  fi
}

function prompt_pbrisbin_setup() {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  add-zsh-hook precmd prompt_pbrisbin_precmd

  zstyle ':prezto:module:editor' completing '...'
  zstyle ':prezto:module:editor:keymap' alternate '<<<'

  zstyle ':prezto:module:git' branch    '%F{red}%b%f'
  zstyle ':prezto:module:git' added     '%F{yellow}+%f'
  zstyle ':prezto:module:git' deleted   '%F{red}x%f'
  zstyle ':prezto:module:git' modified  '%F{green}*%f'
  zstyle ':prezto:module:git' renamed   '%F{yellow}âžœ%f'
  zstyle ':prezto:module:git' unmerged  '%F{red}!%f'
  zstyle ':prezto:module:git' untracked '%F{cyan}+%f'
  zstyle ':prezto:module:git' info \
    'prompt'  ' %%B%F{cyan}(%f%b%%B%F{cyan})%f%%b' \
    'rprompt' ' %%B%U%a%d%r%m%u%%b'

   PROMPT=' %F{cyan}%c%f${git_info_fast[prompt]} '
  RPROMPT='${editor_info[keymap]} %(?::%B%F{red}%? %f%b) ${git_info_fast[rprompt]} '
  SPROMPT='correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_pbrisbin_setup "$@"
